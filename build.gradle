buildscript {
    ext.kotlin_version = '1.1.51'
    repositories {
        mavenCentral()
        maven { url 'http://dl.bintray.com/jetbrains/intellij-plugin-service' }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id 'org.jetbrains.intellij' version '0.2.11'
}

apply plugin: 'java'
apply plugin: 'kotlin'

sourceCompatibility = 1.8
def hasPythonPlugin = project.hasProperty('pythonPlugin')

// intellij configuration
intellij {
    version ideaVersion
    pluginName 'intellij-odoo'
    downloadSources true
    plugins = []
    if (hasPythonPlugin) {
        plugins += [pythonPlugin]
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlin_version"
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

task copyStubs(dependsOn: prepareSandbox, type: Copy) {
    from(projectDir) {
        include "typehints/"
        include "scripts/"
    }
    into "${intellij.sandboxDirectory}/plugins/intellij-odoo"
}

buildPlugin.dependsOn copyStubs
runIde.dependsOn copyStubs
